<Программа> ::= "Script" <Идентификатор> "{"{<Операция>}"}"

<Идентификатор> ::= (<Буква> | "_") {(<Буква> | <Цифра> | "_")}
  
<Цифра> ::= "0"..."9"

<Буква> ::= <НижнийРегистр> | <ВерхнийРегистр>

<НижнийРегистр> ::= "a"..."z" | "а"..."я"
  
<ВерхнийРегистр> ::= "A"..."Z" | "А"..."Я"

<Операция> ::= (
			<ОперацияИнициализации>
			| <ОперацияПрисвоения>
            | <ОперацияВыбора>
            | <ОперацияЦыкла>
            | <ОперацияДвижениеВперед>
            | <ОперацияДвижениеНазад>
            | <ОперацияПоворотЛево>
            | <ОперацияПоворотПраво>
            | <ОперацияВзять>
            | <ОперацияПоложить>
            | <ОперацияЗапечатать>
            )
<ОперацияИнициализации> ::= <Идентификатор> ";"

<ОперацияПрисвоения> ::= <Идентификатор> "=" (<Идентификатор> | <Данные>) ";"

<ОперацияВыбора> ::= "ЕСЛИ" "(" (<БулевоВыражение> | <БулевоЗначение>) ")" ":" {<Операция>} 
				[{"ТОГДА" "(" (<БулевоВыражение> | <БулевоЗначение>) ")" ":" {<Операция>}}] 
				["ИНАЧЕ" ":" {<Операция>}]

<ОперацияЦыкла> ::= "ВЫПОЛНИТЬ" (<Идентификатор> | <Данные>) "РАЗ" ":" {<Операция>} 

<ОперацияДвижениеВперед> ::= "ДвижениеВперед" "На" <ЧисловыеДанные> ";"

<ОперацияДвижениеНазад> ::= "ДвижениеНазад" "На" <ЧисловыеДанные> ";"

<ОперацияПоворотЛево> ::= "ПоворотЛево" "На" <ЧисловыеДанные> ";"

<ОперацияПоворотПраво> ::= "ПоворотПраво" "На" <ЧисловыеДанные> ";"

<ОперацияВзять> ::= "Взять" <ЧисловыеДанные> ";"

<ОперацияПоложить> ::= "Положить" "На" <ЧисловыеДанные> ";"

<ОперацияЗапечатать> ::= "Запечатать" "В" <ЧисловыеДанные> ";"

<Данные> ::= <Буква> 
			| <Строка> 
			| <Цифра> 
			| <Число> 
			| <ЧислоL> 
			| <ЧислоСТочкой> 

<Строка> ::= '"' {(<Буква> | <Символ> | <СимволЭкранирования> | <Цифра>)} '"'

<Символ> ::= <любой исходный символ, кроме «\» или новой строки>

<СимволЭкранирования> ::= "\" <любой символ ASCII>

<Число> ::= [- | +] {<Цифра>}

<ЧислоL> ::= <Число> ("l" | "L")

<ЧислоСТочкой> ::= <ФормаСТочкой> | <ФормаСЭкспонентой>
  
<ФормаСТочкой> ::= (<Число> | <ЧислоL>) "." (<Число> | <ЧислоL>)
  
<ФормаСЭкспонентой> ::= ( (<Число> | <ЧислоL>) | <ФормаСТочкой>) <Экспонентa>
            
<Экспонентa> ::= ("e" | "E") <Число>

<ЧисловыеДанные> ::= <Цифра> | Число | <ЧислоL> | <ЧислоСТочкой>

<БулевоЗначение> ::= ("true" | "false") | ( "1" | "0" )

<БулевоВыражение> ::= (<Данные> | <Идентификатор> | <АрифметическоеВыражение> | <БулевоВыражение> | <БулевоЗначение>)
				<ЛогическийОператор> 
				(<Данные> | <Идентификатор> | <АрифметическоеВыражение> | <БулевоВыражение> | <БулевоЗначение>)

<АрифметическоеВыражение> ::= (<Идентификатор> | <ЧисловыеДанные>)
					<АрифметическийОператор> 
					(<Идентификатор> | <ЧисловыеДанные> | <АрифметическоеВыражение>)

<ЛогическийОператор> ::= "РАВНО" | "МЕНЬШЕ" 
				| "БОЛЬШЕ" 
				| "МЕНЬШЕилиРАВНО" | "БОЛЬШЕилиРАВНО" 
				| "НЕРАВНО"

<АрифметическийОператор> ::= "+" |"-" 
					| "*" | "/" 
					| "%"
